// Gradle script to build the SkyControl project

plugins {
    id 'base' // to add a "clean" task to the root project
}

ext {
    jmeTarget = '' // distinguish non-JME libraries built for specific JME releases
    skySnapshot = '-SNAPSHOT' // for development builds
    //skySnapshot = '' // for release builds
    skycontrolVersion = '1.1.1' + jmeTarget + skySnapshot
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of snapshots
}

tasks.register('AppChooser') {
    dependsOn ':SkyExamples:AppChooser'
    description = 'Runs the example apps.'
}

tasks.register('checkstyle') {
    dependsOn ':SkyAssets:checkstyleMain', ':SkyExamples:checkstyleMain', \
        ':SkyLibrary:checkstyleMain', ':SkyLibrary:checkstyleTest'
    description = 'Checks the style of all Java sourcecode.'
}

// Register publishing tasks:

tasks.register('install') {
    dependsOn ':SkyLibrary:install'
    description = 'Installs the Maven artifacts to the local repository.'
}
tasks.register('release') {
    dependsOn ':SkyLibrary:release'
    description = 'Stages the Maven artifacts to the Central Publisher Portal.'
}
