// Gradle build script for the SkyControl project

plugins {
    id 'base' // to add a "clean" task to the root project
}

ext {
    jmeTarget = '' // distinguish non-JME libraries built for specific JME releases

    // module coordinates of external dependencies:
    acorusCoordinates = 'com.github.stephengold:Acorus:1.0.0' + jmeTarget
    heartCoordinates = 'com.github.stephengold:Heart:8.4.0' + jmeTarget
    jcommanderCoordinates = 'com.beust:jcommander:1.82'
    jme3utilitiesniftyCoordinates = 'com.github.stephengold:jme3-utilities-nifty:0.9.35'

    // current versions of libraries:
    jme3Version = '3.6.0-stable'
    skycontrolVersion = '1.0.5-SNAPSHOT'
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_20)) {
        // Suppress warnings that source value 8 is obsolete.
        options.compilerArgs << '-Xlint:-options'
    }
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_10)) {
        options.release = 8
    }
}

tasks.withType(JavaExec) { // Java runtime options:
    enableAssertions true
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xms512m', '-Xmx512m' // to enlarge the Java heap
    //jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of SNAPSHOTs
}

tasks.register('checkstyle') {
    dependsOn ':SkyAssets:checkstyleMain', ':SkyExamples:checkstyleMain', \
        ':SkyLibrary:checkstyleMain', ':SkyLibrary:checkstyleTest'
    description 'Checks the style of all Java sourcecode.'
}

tasks.register('AppChooser') {
    dependsOn ':SkyExamples:AppChooser'
    description 'Runs the example apps.'
}

// publishing tasks:

tasks.register('install') {
    dependsOn ':SkyLibrary:install'
    description 'Installs Maven artifacts to the local repository.'
}
tasks.register('release') {
    dependsOn ':SkyLibrary:release'
    description 'Stages Maven artifacts to Sonatype OSSRH.'
}