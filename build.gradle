// Gradle script to build the SkyControl project

plugins {
    id 'base' // to add a "clean" task to the root project
}

ext {
    skySnapshot = '-SNAPSHOT' // for development builds
    //skySnapshot = '' // for release builds

    jmeTarget = '' // distinguish non-JME libraries built for specific JME releases

    // module coordinates of external dependencies:
    acorusCoordinates = 'com.github.stephengold:Acorus:2.0.0' + jmeTarget
    heartCoordinates = 'com.github.stephengold:Heart:9.0.0' + jmeTarget
    jcommanderCoordinates = 'com.beust:jcommander:1.82'
    jme3utilitiesniftyCoordinates = 'com.github.stephengold:jme3-utilities-nifty:0.9.37'

    // current versions of libraries:
    jme3Version = '3.7.0-beta1'
    skycontrolVersion = '1.0.6' + jmeTarget + skySnapshot
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of snapshots
}

tasks.register('checkstyle') {
    dependsOn ':SkyAssets:checkstyleMain', ':SkyExamples:checkstyleMain', \
        ':SkyLibrary:checkstyleMain', ':SkyLibrary:checkstyleTest'
    description 'Checks the style of all Java sourcecode.'
}

tasks.register('AppChooser') {
    dependsOn ':SkyExamples:AppChooser'
    description 'Runs the example apps.'
}

// Register publishing tasks:

tasks.register('install') {
    dependsOn ':SkyLibrary:install'
    description 'Installs Maven artifacts to the local repository.'
}
tasks.register('release') {
    dependsOn ':SkyLibrary:release'
    description 'Stages Maven artifacts to Sonatype OSSRH.'
}
